plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.1.0'
    id 'com.datavitec.runtime' version '1.13.1-patch-2'
    id 'io.github.file5.guidesigner' version '1.0.2'
}

group = 'pw.wunderlich.lightbeat'
version = '1.8.0-SNAPSHOT'
description = 'LightBeat'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

application {
    mainClass = 'pw.wunderlich.lightbeat.LightBeat'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    mockitoAgent
}

dependencies {
    implementation 'io.github.zeroone3010:yetanotherhueapi:2.8.0-lb' // github.com/Kakifrucht/yetanotherhueapi -> install to maven local
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'org.slf4j:jul-to-slf4j:2.0.17'
    implementation 'com.github.wendykierp:JTransforms:3.1'
    implementation 'com.github.weisj:darklaf-core:3.1.1'
    implementation 'com.jetbrains.intellij.java:java-gui-forms-rt:252.26199.158'
    implementation 'org.jitsi:libjitsi:1.1-34-gb93ce2ee'

    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    mockitoAgent('org.mockito:mockito-core:5.20.0') {
        transitive = false
    }
}

tasks.named('instrumentForms') {
    inputs.files(sourceSets.main.allSource.filter { it.name.endsWith('.form') })
    outputs.dir(sourceSets.main.output.classesDirs)
}

boolean isWindowsBuild = project.hasProperty('windows-msi')
boolean isMacBuild = project.hasProperty('macos-dmg')
boolean isLinuxBuild = project.hasProperty('linux-deb')

// If no property is specified, fall back to detecting the OS
if (!isWindowsBuild && !isMacBuild && !isLinuxBuild) {
    String os = System.getProperty("os.name").toLowerCase()
    if (os.contains("win")) isWindowsBuild = true
    if (os.contains("mac")) isMacBuild = true
    if (os.contains("nix") || os.contains("nux")) isLinuxBuild = true
}

def libjitsiAddOpensArgs = [ '--add-opens', 'java.base/java.lang=ALL-UNNAMED' ]
runShadow {
    jvmArgs libjitsiAddOpensArgs
    if (project.hasProperty('dumpAll')) {
        systemProperty 'lightbeat.audio.dumpAll', 'true'
    }
}

shadowJar {
    archiveBaseName = 'LightBeat'
    manifest {
        attributes(
                'Main-Class': application.getMainClass(),
                'Implementation-Version': project.version
        )
    }
    exclude 'jpackage/*'
    exclude '*.sh'
}

tasks.register('bundleReleaseZip', Zip) {
    dependsOn shadowJar
    group = 'Distribution'
    description = 'Bundles the application into a portable ZIP file with an app subfolder.'

    archiveFileName = "LightBeat-${project.version}.zip"
    destinationDirectory = layout.buildDirectory.dir('distributions')

    rootSpec.into(project.name) {
        into('app') {
            from(shadowJar.archiveFile)
        }
        from('src/main/resources/launch_lightbeat.sh') {
            filePermissions {
                unix('0755')
            }
        }
        from('src/main/resources/launch_lightbeat.bat')
        from('THIRD_PARTY_LICENSES.md')
    }
}

jpackageImage {
    doLast {
        copy {
            from('THIRD_PARTY_LICENSES.md')
            into(layout.buildDirectory.dir("jpackage/${project.name}"))
        }
    }
}

runtime {
    launcher {
        def customJvmArgs = new ArrayList(libjitsiAddOpensArgs)
        if (project.hasProperty('updatechecker-disabled')) {
            customJvmArgs.add('-Dupdatechecker.disabled=true')
        }

        jvmArgs = customJvmArgs
    }

    modules = ['jdk.crypto.ec']
    additive = true

    jpackage {

        // Windows MSI
        if (isWindowsBuild) {
            installerType = 'msi'
            imageOptions = [
                    '--icon', 'src/main/resources/jpackage/icon.ico',
                    '--add-launcher', "${project.name}-console=src/main/resources/jpackage/debug_build.properties",
            ]
            installerOptions = [
                    '--vendor', 'wunderlich.pw',
                    '--win-menu',
                    '--win-menu-group', project.name,
                    '--win-shortcut',
                    '--win-upgrade-uuid', 'c7d7f7e9-7b5c-4f1a-9a0e-8f2c3d4b5e6f',
            ]
        }
        // macOS DMG Configuration
        else if (isMacBuild) {
            installerType = 'dmg'
            imageOptions = [
                    '--icon', 'src/main/resources/jpackage/icon.icns'
            ]
            installerOptions = [
                    '--vendor', 'wunderlich.pw',
                    '--mac-package-name', project.name,
            ]
        }
        // Linux DEB Configuration
        else if (isLinuxBuild) {
            installerType = 'deb'
            imageOptions = [
                    '--icon', 'src/main/resources/jpackage/icon.png'
            ]
            installerOptions = [
                    '--vendor', 'wunderlich.pw',
                    '--linux-deb-maintainer', 'lightbeat@wunderlich.pw',
                    '--linux-menu-group', 'Audio',
                    '--linux-app-category', 'Audio',
                    '--linux-shortcut'
            ]
        }
    }
}

test {
    useJUnitPlatform()
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

compileJava {
    options.encoding = 'UTF-8'
}
